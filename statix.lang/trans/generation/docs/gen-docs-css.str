module generation/docs/gen-docs-css

imports
  
  libstratego-lib
  
  libspoofax/core/-
  libspoofax/resource/-
  EditorService

rules // generate docs highlighting CSS for meta-language from its editor/Colorer.esv file

  generate-docs-css: (_, _, ast, path, project-path) -> ()
    with
      meta-language := <get-extension> path
    ; repo-clone := <local-path; dirname> project-path
    ; repo-path := <local-path; dirname; dirname> project-path
    ; meta-language-project-path := $[[repo-path]/[<meta-project-path> meta-language]]
    ; colorer-file := 
        <!$[[meta-language-project-path]/editor/Colorer-CF.esv]; file-exists 
         <+ !$[[meta-language-project-path]/editor/Colorer.esv]>
    ; css-file := $[[repo-clone]/docs/stylesheets/[meta-language].css]
    ; say(!css-file; string-replace(|repo-path, "..."))
    ; css := <parse-file; colorer-to-css(|meta-language)> colorer-file
    ; outs := <fopen> (css-file, "w")
    ; <fputs> (css, outs)
    ; <fclose> outs
    ; metaborg-css-dir := $[[repo-clone]/docs/stylesheets]
    ; metaborg-css-file :=  $[[meta-language-project-path]/trans/generation/docs/metaborg.css]
    ; say(!$[[metaborg-css-dir]/metaborg.css]; string-replace(|repo-path, "..."))
    ; <copy-file> (metaborg-css-file, metaborg-css-dir)

  meta-project-path: "sdf3" -> "sdf/org.metaborg.meta.lang.template"
  meta-project-path: "nab"  -> "nabl/org.metaborg.meta.lang.nabl"
  meta-project-path: "stx"  -> "nabl/statix.lang"
  meta-project-path: "str"  -> "stratego/stratego.lang"

  colorer-to-css(|meta-language) =
      collect-om(?ColorRule(_, _))
    ; map(color-rule-to-css-rule(|meta-language))
    ; concat-strings
    ; repeat(string-replace(|"\n  \n", "\n") | 3)
  
  color-rule-to-css-rule(|meta-language):
    ColorRule(kind, Attribute(color, bg-color, font)) ->
    $[[<gen-selector(|meta-language)> kind] {
        [<gen-color> color]
        [<gen-bg-color> bg-color]
        [<gen-font-weight> font]
        [<gen-font-style> font]
      }
      ]

  gen-selector(|meta-language): Token(TK_KEYWORD())    -> $[.[meta-language] .keyword]
  gen-selector(|meta-language): Token(TK_IDENTIFIER()) -> $[.[meta-language] .identifier]
  gen-selector(|meta-language): Token(TK_STRING())     -> $[.[meta-language] .string]
  gen-selector(|meta-language): Token(TK_NUMBER())     -> $[.[meta-language] .number]
  gen-selector(|meta-language): Token(TK_VAR())        -> $[.[meta-language] .var]
  gen-selector(|meta-language): Token(TK_OPERATOR())   -> $[.[meta-language] .operator]
  gen-selector(|meta-language): Token(TK_LAYOUT())     -> $[.[meta-language] .layout]
  
  gen-selector(|meta-language):
    ConstructorOnly(Constructor(c)) -> $[.[meta-language] .cons_[c], .[meta-language] .cons_[c] > * > .token]
  gen-selector(|meta-language):
    Sort(s) -> $[.[meta-language] .sort_[s]]
  gen-selector(|meta-language):
    SortAndConstructor(Sort(s), Constructor(c)) -> $[.[meta-language] .sort_[s].cons_[c]]
  
  gen-color: ColorRGB(r, g, b) -> $[color: rgb([r] [g] [b]);]
  gen-color: ColorDefault()    -> $[color: var(--md-code-fg-color);]
  gen-color: NoColor()         -> ""

  gen-bg-color: ColorRGB(r, g, b) -> $[background-color: rgb([r] [g] [b]);]
  gen-bg-color: ColorDefault()    -> $[background-color: var(--md-code-bg-color);]
  gen-bg-color: NoColor()         -> ""

  gen-font-weight: BOLD()        -> "font-weight: bold;"
  gen-font-weight: BOLD_ITALIC() -> "font-weight: bold;"
  gen-font-weight: ITALIC()      -> "font-weight: normal;"
  gen-font-weight: NORMAL()      -> "font-weight: normal;"

  gen-font-style: BOLD()        -> "font-style: normal;"
  gen-font-style: BOLD_ITALIC() -> "font-style: italic;"
  gen-font-style: ITALIC()      -> "font-style: italic;"
  gen-font-style: NORMAL()      -> "font-style: normal;"
  